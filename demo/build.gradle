if (isRelease) {
    apply plugin: 'com.android.library' // 正式环境  library不能独立运行
} else {
    apply plugin: 'com.android.application' // 测试环境 application独立运行
}
apply plugin: 'kotlin-android'
apply plugin: 'kotlin-android-extensions'
apply plugin: 'kotlin-kapt'

def androidID = rootProject.ext.androidID
android {
    compileSdkVersion androidID.compileSdkVersion
    defaultConfig {
        multiDexEnabled true
        if (!isRelease) {  // 能够独立运行 必须要有appID
            applicationId appID.demo // 组件化模式能独立运行才能有applicationId
        }
        minSdkVersion androidID.minSdkVersion
        targetSdkVersion androidID.targetSdkVersion
        versionCode androidID.versionCode
        versionName androidID.versionName
        // 组件化 和 集成化
        buildConfigField("boolean", "isRelease", String.valueOf(isRelease))
        consumerProguardFiles 'consumer-rules.pro'

        externalNativeBuild {
            cmake {
                // cppFlags "" // 默认五大平台
                // 指定CPU架构，Cmake的本地库， 例如：VoiceChange ---> armeabi-v7a
                abiFilters 'armeabi-v7a' , 'arm64-v8a'
            }
        }

        // 指定CPU架构，打入APK lib/CPU平台
        ndk {
            abiFilters 'armeabi-v7a' , 'arm64-v8a'
        }
    }

    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
        }
    }

    externalNativeBuild {
        cmake {
            path "src/main/cpp/CMakeLists.txt"
            version "3.10.2"
        }
    }

    sourceSets {
        main {
            if (!isRelease) {
                // 如果是组件化模式，需要单独运行时 Debug
                manifest.srcFile 'src/main/debug/AndroidManifest.xml'
                java.srcDirs += "src/main/debug/java/"
            } else { // 正式环境下
                // 集成化模式，整个项目打包apk
                manifest.srcFile 'src/main/AndroidManifest.xml'

                java {
                    // release 时 debug 目录下文件不需要合并到主工程
                    exclude "**/debug/**"
                }
            }
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_8
        targetCompatibility JavaVersion.VERSION_1_8
    }
    kotlinOptions {
        jvmTarget = '1.8'
    }
    dataBinding {
        enabled true
    }
    ndkVersion '21.1.6352462'

    if (isRelease) {
        //给模块设置不同的资源前缀，可以避免重复命名
        resourcePrefix "demo_"
    }
}

apply plugin: 'aop.trace.plugin'
buildscript {
    repositories {
        google()
        jcenter()
        //自定义插件maven地址
        maven { url '../repo/aop_trace_repo' }
    }
    dependencies {
        //加载自定义插件 group + module + version
        classpath 'com.walker.plugin:aop_trace_plugin:1.0.0'
    }
}

AopTraceExt {
    enable true
    tracePrefix["com/walker/"]
}

dependencies {
    implementation "org.jetbrains.kotlin:kotlin-stdlib-jdk7:$rootProject.ext.kotlin_version"
    implementation "androidx.appcompat:appcompat:$rootProject.ext.androidx_version"
    implementation 'com.google.android.material:material:1.1.0'

    implementation "androidx.multidex:multidex:$rootProject.ext.multidex_version"
    implementation 'androidx.core:core-ktx:1.3.2'
    implementation "androidx.constraintlayout:constraintlayout:$rootProject.ext.constraint_layout_verson"
    implementation "androidx.work:work-runtime-ktx:$rootProject.ext.workVersion"

    implementation 'com.scwang.smartrefresh:SmartRefreshLayout:1.1.0-alpha-7'
    implementation 'com.scwang.smartrefresh:SmartRefreshHeader:1.1.0-alpha-7'//没有使用特殊Header，可以不加这行
    implementation "androidx.recyclerview:recyclerview:1.1.0"

    implementation project(':common')
    implementation project(':network')
    implementation project(':webview')

    implementation "org.jetbrains.kotlinx:kotlinx-coroutines-android:$rootProject.ext.kotlin_coroutines_version"

    annotationProcessor "com.google.auto.service:auto-service:$rootProject.ext.autoservice_version"
    implementation "com.google.auto.service:auto-service:$rootProject.ext.autoservice_version"
    kapt "com.google.auto.service:auto-service:$rootProject.ext.autoservice_version"

    //retrofit
    implementation "com.squareup.retrofit2:retrofit:$rootProject.ext.retrofit_version"
    implementation "com.squareup.retrofit2:converter-gson:$rootProject.ext.retrofit_version"

    implementation 'androidx.paging:paging-runtime:3.0.0-beta01'
    //fmd的jar包
    implementation files("libs\\fmod.jar")
    //apache httpclient的jar包
    implementation files("libs\\org.apache.http.legacy.jar")
    //vcard
    implementation ('com.googlecode.ez-vcard:ez-vcard:0.12.0'){
        exclude group: 'org.jsoup'
        exclude group: 'org.freemarker'
        exclude group: 'com.fasterxml.jackson.core'
    }
    //二维码识别
    implementation 'com.github.yuzhiqiang1993:zxing:2.2.9'
    //Subsampling Scale Image View
    implementation 'com.davemorrissey.labs:subsampling-scale-image-view-androidx:3.10.0'
}